BACKUP ~tb#kits/backup~
AUTHOR ~The Bigg (vbigiani@gmail.com)~
VERSION @0

README ~tb#kits/tra/%LANGUAGE%/readme.html~

LANGUAGE ~english~
	 ~english~
	 ~tb#kits/tra/english/setup.tra~

BEGIN @1

ADD_PROJECTILE ~tb#kits/tb#kav/pro/tb#kavbu.pro~

COPY ~tb#kits/tb#kav/res~ ~override~

COPY ~tb#kits/tb#kav/spl/tb#kavbc.spl~ ~override~
	SAY NAME1 @2
	FOR(i = 1; i < 20; i += 1) BEGIN
		READ_LONG  0x64 header_off
		READ_SHORT 0x68 header_cnt
		READ_LONG  0x6a effect_off
		READ_SHORT header_off + 0x1e effect_cnt
	
		READ_ASCII header_off base_header (0x28)
		READ_ASCII effect_off base_effects (0x30 * effect_cnt)
		INSERT_BYTES (header_off + 0x28 * header_cnt) 0x28
		WRITE_LONG 0x6a 0x28 + effect_off
		WRITE_SHORT 0x68 header_cnt + 1

		WRITE_ASCIIE (header_off + 0x28 * header_cnt) ~%base_header%~ (0x28)
		WRITE_SHORT (header_off + 0x28 * header_cnt) + 0x10    1 + i
		WRITE_SHORT (header_off + 0x28 * header_cnt) + 0x20    i * effect_cnt
		
		
		READ_LONG  0x6a effect_off
		INSERT_BYTES (effect_off + 0x30 * i * effect_cnt)  0x30 * effect_cnt
		WRITE_ASCIIE (effect_off + 0x30 * i * effect_cnt) ~%base_effects%~ (0x30 * effect_cnt)
		
		FOR (j = i * effect_cnt; j < (i+1) * effect_cnt; j += 1) BEGIN
			READ_SHORT (effect_off + 0x30 * j) effect
			PATCH_IF effect = 18 BEGIN
				WRITE_LONG (effect_off + 0x30 * j) + 0x04 6 + i * 2
			END
		END
	END

COPY ~tb#kits/tb#kav/spl/tb#kavbu.spl~ ~override~
	WRITE_SHORT 0x98 %tb#kavbu%
	FOR(i = 1; i < 5; i += 1) BEGIN
		READ_LONG  0x64 header_off
		READ_SHORT 0x68 header_cnt
		READ_LONG  0x6a effect_off
		READ_SHORT header_off + 0x1e effect_cnt
	
		READ_ASCII header_off base_header (0x28)
		READ_ASCII effect_off base_effects (0x30 * effect_cnt)
		INSERT_BYTES (header_off + 0x28 * header_cnt) 0x28
		WRITE_LONG 0x6a 0x28 + effect_off
		WRITE_SHORT 0x68 header_cnt + 1

		WRITE_ASCIIE (header_off + 0x28 * header_cnt) ~%base_header%~ (0x28)
		WRITE_SHORT (header_off + 0x28 * header_cnt) + 0x10    1 + i * 4
		WRITE_SHORT (header_off + 0x28 * header_cnt) + 0x20    i * effect_cnt
		
		
		READ_LONG  0x6a effect_off		
		INSERT_BYTES (effect_off + 0x30 * i * effect_cnt)  0x30 * effect_cnt
		WRITE_ASCIIE (effect_off + 0x30 * i * effect_cnt) ~%base_effects%~ (0x30 * effect_cnt)
		
		FOR (j = i * effect_cnt; j < (i+1) * effect_cnt; j += 1) BEGIN
			READ_LONG (effect_off + 0x30 * j) + 0x1c dices
			PATCH_IF dices != 0 BEGIN
				WRITE_LONG (effect_off + 0x30 * j) + 0x1c dices + i
			END

			READ_LONG (effect_off + 0x30 * j) + 0x28 savebonus
			PATCH_IF savebonus != 0 BEGIN
				WRITE_LONG (effect_off + 0x30 * j) + 0x28 savebonus - i
			END
		END
	END

COPY ~tb#kits/tb#kav/spl/tb#kavlg.spl~ ~override~
	WRITE_SHORT 0x98 %tb#kavbu%
	SAY NAME1 @3
	SAY UNIDENTIFIED_DESC @4

ADD_KIT ~TB#KAV~
	// appended to CLASWEAP.2da
	~TB#KAV             1           1           1           1           1           1           1           1~
	~TB#KAV 5 1 0 5 5 1 1 0 1 1 1 1 5 1 1 1 1 1 5 5 1 1 5 0 0 0 0 0 2 0 0 0 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5~
	// appended column-wise to WEAPPROF.2da
	~TB#KAV                9       0       0       0       0       0~
	// appended to ABCLASRQ.2da
	~TB#KAV                0       0       0       0       0       0~
	// appended to ABDCDSRQ.2da
	~TB#KAV                17      0       0       0       0       0~
	// appended to ABDCSCRQ.2da
	~TB#KAV                15      0       0       0       0       0~
	// appended to ALIGNMNT.2da
	~TB#KAV                1       1       1       1       1       1       1       1       1~
	// appended to DUALCLAS.2da
	~TB#KAV                1       0       1       1       1       1~
	// path to your CLAB-style 2da file 
	~tb#kits/tb#kav/res/TB#KAV.2da~
	// PC races and classes allowed to use this kit, from KITTABLE.2da
	~K_F_D K_F_E K_F_G K_F_H K_F_HE K_F_HL K_F_HO~
	// usability flags, added to the end of the KITLIST.2da entry
	~0x00000004     2~
	// HLA table to use; see LUABBR.2da
	~tb0~
	// list of starting equipment for PCs starting in ToB. Appended column-wise to 25STWEAP.2da
	~* * HELM19 BAG22 RING06 RING29 CLCK02 BOOT01 AMUL19 BRAC14 BELT06 * * * POTN52,5 POTN04,2 POTN14,5 HAMM07 SW1H27 STAF08~
	
	// Lower, mixed, and help kit names and descriptions, added to KITLIST.2da 
	SAY @5
	SAY @1
	SAY @6


BEGIN @7
REQUIRE_COMPONENT ~tb#kits/tb#kits.tp2~ 0 @8
REQUIRE_COMPONENT ~HAIASS/SETUP-HAIASS.TP2~ 0 @9

<<<<<<<< .../haiass.baf
IF
  Global("tb#HaiassVanguard","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("tb#HaiassVanguard","GLOBAL",1)
    AddSpecialAbility("tb#kavbc")
END

//********************************* Defenderse (HaiassTactica 2) ************
IF
  AttackedBy([EVILCUTOFF],DEFAULT)
  Global("HaiassTactica","GLOBAL",2)
  HaveSpellRES("tb#kavbc")
THEN
  RESPONSE #100
    SpellRES("tb#kavbc", LastAttackerOf(Myself))
END

//********************* Atacar lanzadores de conjuros (HaiassTactica 3) *****
IF
  Global("HaiassTactica","GLOBAL",3)
  Global("HaiassQuieto","GLOBAL",0)
  OR(4)
    See(NearestEnemyOfType([0.GENERAL_ITEM.0.MAGE_ALL]))
    See(NearestEnemyOfType([0.GENERAL_ITEM.0.CLERIC_ALL]))
    See(NearestEnemyOfType([0.GENERAL_ITEM.0.DRUID_ALL]))
    See(NearestEnemyOfType([0.GENERAL_ITEM.0.BARD_ALL]))
  HaveSpellRES("tb#kavbc")
THEN
  RESPONSE #100
    SpellRES("tb#kavbc", LastSeenBy(Myself))
END

IF
  See(NearestEnemyOf(Myself))
  Global("HaiassTactica","GLOBAL",3)
  HaveSpellRES("tb#kavbc")
THEN
  RESPONSE #100
    SpellRES("tb#kavbc", Nearestenemyof(Myself))
END

//************************* Defender a CHARNAME (HaiassTactica 4)************
IF
  See(Protagonist)
  Exists(LastAttackerOf(Protagonist))
  Global("HaiassTactica","GLOBAL",4)
  Global("HaiassQuieto","GLOBAL",0)
  HaveSpellRES("tb#kavbc")
THEN
  RESPONSE #100
    SpellRES("tb#kavbc", LastAttackerOf(Protagonist))
END

IF
  AttackedBy([EVILCUTOFF],DEFAULT)
  Global("HaiassTactica","GLOBAL",4)
  HaveSpellRES("tb#kavbc")
THEN
  RESPONSE #100
    SpellRES("tb#kavbc", LastAttackerOf(Myself))
END

//*************************** Atacar enemigos (HaiassTactica 0) *************
IF
  AttackedBy([EVILCUTOFF],DEFAULT)
  !Global("HaiassTactica","GLOBAL",4)
  !Global("HaiassTactica","GLOBAL",3)
  !Global("HaiassTactica","GLOBAL",2)
  !Global("HaiassTactica","GLOBAL",1)
  HaveSpellRES("tb#kavbc")
THEN
  RESPONSE #100
    SpellRES("tb#kavbc", LastAttackerOf(Myself))
END

IF
  Global("HaiassQuieto","GLOBAL",0)
  See(NearestEnemyOf(Protagonist))
  !Global("HaiassTactica","GLOBAL",4)
  !Global("HaiassTactica","GLOBAL",3)
  !Global("HaiassTactica","GLOBAL",2)
  !Global("HaiassTactica","GLOBAL",1)
  HaveSpellRES("tb#kavbc")
THEN
  RESPONSE #100
    SpellRES("tb#kavbc", NearestEnemyOf(Protagonist))
END

>>>>>>>>

EXTEND_TOP ~haiass.bcs~ ~.../haiass.baf~
