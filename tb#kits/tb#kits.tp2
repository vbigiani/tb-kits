BACKUP ~tb#kits/backup~
AUTHOR ~The Bigg (vbigiani@gmail.com)~
VERSION ~1.0~

README ~tb#kits/readme.html~

BEGIN ~Biotic Vanguard~

ADD_PROJECTILE ~tb#kits/tb#kav/pro/tb#kavbu.pro~

COPY ~tb#kits/tb#kav/res~ ~override~

COPY ~tb#kits/tb#kav/spl/tb#kavbc.spl~ ~override~
	SAY NAME1 ~Biotic Charge~
	FOR(i = 1; i < 20; i += 1) BEGIN
		READ_LONG  0x64 header_off
		READ_SHORT 0x68 header_cnt
		READ_LONG  0x6a effect_off
		READ_SHORT header_off + 0x1e effect_cnt
	
		READ_ASCII header_off base_header (0x28)
		READ_ASCII effect_off base_effects (0x30 * effect_cnt)
		INSERT_BYTES (header_off + 0x28 * header_cnt) 0x28
		WRITE_LONG 0x6a 0x28 + effect_off
		WRITE_SHORT 0x68 header_cnt + 1

		WRITE_ASCIIE (header_off + 0x28 * header_cnt) ~%base_header%~ (0x28)
		WRITE_SHORT (header_off + 0x28 * header_cnt) + 0x10    1 + i
		WRITE_SHORT (header_off + 0x28 * header_cnt) + 0x20    i * effect_cnt
		
		
		READ_LONG  0x6a effect_off
		INSERT_BYTES (effect_off + 0x30 * i * effect_cnt)  0x30 * effect_cnt
		WRITE_ASCIIE (effect_off + 0x30 * i * effect_cnt) ~%base_effects%~ (0x30 * effect_cnt)
		
		FOR (j = i * effect_cnt; j < (i+1) * effect_cnt; j += 1) BEGIN
			READ_SHORT (effect_off + 0x30 * j) effect
			PATCH_IF effect = 18 BEGIN
				WRITE_LONG (effect_off + 0x30 * j) + 0x04 6 + i * 2
			END
		END
	END

COPY ~tb#kits/tb#kav/spl/tb#kavbu.spl~ ~override~
	WRITE_SHORT 0x98 %tb#kavbu%
	FOR(i = 1; i < 5; i += 1) BEGIN
		READ_LONG  0x64 header_off
		READ_SHORT 0x68 header_cnt
		READ_LONG  0x6a effect_off
		READ_SHORT header_off + 0x1e effect_cnt
	
		READ_ASCII header_off base_header (0x28)
		READ_ASCII effect_off base_effects (0x30 * effect_cnt)
		INSERT_BYTES (header_off + 0x28 * header_cnt) 0x28
		WRITE_LONG 0x6a 0x28 + effect_off
		WRITE_SHORT 0x68 header_cnt + 1

		WRITE_ASCIIE (header_off + 0x28 * header_cnt) ~%base_header%~ (0x28)
		WRITE_SHORT (header_off + 0x28 * header_cnt) + 0x10    1 + i * 4
		WRITE_SHORT (header_off + 0x28 * header_cnt) + 0x20    i * effect_cnt
		
		
		READ_LONG  0x6a effect_off		
		INSERT_BYTES (effect_off + 0x30 * i * effect_cnt)  0x30 * effect_cnt
		WRITE_ASCIIE (effect_off + 0x30 * i * effect_cnt) ~%base_effects%~ (0x30 * effect_cnt)
		
		FOR (j = i * effect_cnt; j < (i+1) * effect_cnt; j += 1) BEGIN
			READ_LONG (effect_off + 0x30 * j) + 0x1c dices
			PATCH_IF dices != 0 BEGIN
				WRITE_LONG (effect_off + 0x30 * j) + 0x1c dices + i
			END

			READ_LONG (effect_off + 0x30 * j) + 0x28 savebonus
			PATCH_IF savebonus != 0 BEGIN
				WRITE_LONG (effect_off + 0x30 * j) + 0x28 savebonus - i
			END
		END
	END

COPY ~tb#kits/tb#kav/spl/tb#kavlg.spl~ ~override~
	WRITE_SHORT 0x98 %tb#kavbu%
	SAY NAME1 ~Lift Grenade~
	SAY UNIDENTIFIED_DESC ~Lift Grenade
Powerful Vanguards have learned how to generate strong blows on their enemies without using their own bodies.

Enemies in a 15' radius from the impact point suffer 20d4 crushing damage and are stunned for 6 seconds. A save vs. breath at -2 halves the damage and avoids the stun~

ADD_KIT ~TB#KAV~
	// appended to CLASWEAP.2da
	~TB#KAV             1           1           1           1           1           1           1           1~
	~TB#KAV 5 1 0 5 5 1 1 0 1 1 1 1 5 1 1 1 1 1 5 5 1 1 5 0 0 0 0 0 2 0 0 0 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5~
	// appended column-wise to WEAPPROF.2da
	~TB#KAV                9       0       0       0       0       0~
	// appended to ABCLASRQ.2da
	~TB#KAV                0       0       0       0       0       0~
	// appended to ABDCDSRQ.2da
	~TB#KAV                17      0       0       0       0       0~
	// appended to ABDCSCRQ.2da
	~TB#KAV                15      0       0       0       0       0~
	// appended to ALIGNMNT.2da
	~TB#KAV                1       1       1       1       1       1       1       1       1~
	// appended to DUALCLAS.2da
	~TB#KAV                1       0       1       1       1       1~
	// path to your CLAB-style 2da file 
	~tb#kits/tb#kav/res/TB#KAV.2da~
	// PC races and classes allowed to use this kit, from KITTABLE.2da
	~K_F_D K_F_E K_F_G K_F_H K_F_HE K_F_HL K_F_HO~
	// usability flags, added to the end of the KITLIST.2da entry
	~0x00000004     2~
	// HLA table to use; see LUABBR.2da
	~tb0~
	// list of starting equipment for PCs starting in ToB. Appended column-wise to 25STWEAP.2da
	~* * HELM19 BAG22 RING06 RING29 CLCK02 BOOT01 AMUL19 BRAC14 BELT06 * * * POTN52,5 POTN04,2 POTN14,5 HAMM07 SW1H27 STAF08~
	
	// Lower, mixed, and help kit names and descriptions, added to KITLIST.2da 
	SAY ~biotic vanguard~
	SAY ~Biotic Vanguard~
	SAY ~BIOTIC VANGUARD: By digesting a substance known as Red Sand, these warriors benefit from increased speed, and have developed the ability to forcibly slam their bodies into their enemies, damaging them while healing themselves. However, such a fighting style imposes several sacrifices on the warrior himself, as he can no longer wear armor, and can use single-handed weapons, but only clumsily so.

Advantages:
-  Bonus -1 to speed factor and +1 movement speed for every 4 levels (starting at 1st level)
-  Bonus +1 to hit and -1 to AC for every 6 levels (starting at 3rd level)
-  May use the 'Biotic Charge' ability once per 18 seconds (no daily limit). The warrior is launched at full speed at an enemy, while his wounds are closed and nearby foes are damaged and briefly stunned by the force of the impact:
  -  The warrior gains 6 hit points, plus one for every level (upt to a maximum of 44 hit points at 20th level). If the warrior goes over his maximum hit point total with this ability, he loses it after six seconds.
  -  Enemies in a 15' radius from the selected target suffer the following effects:
    -  1st  level:  2d4 crushing damage, stun for 1 second. Save vs. breath at +4 halves the damage and avoids the stun
    -  5th  level:  4d4 crushing damage, stun for 1 second. Save vs. breath at +3 halves the damage and avoids the stun
    -  9th  level:  6d4 crushing damage, stun for 1 second. Save vs. breath at +2 halves the damage and avoids the stun
    -  13th level:  8d4 crushing damage, stun for 1 second. Save vs. breath at +1 halves the damage and avoids the stun
    -  17th level: 10d4 crushing damage, stun for 1 second. Save vs. breath at +0 halves the damage and avoids the stun
- May choose the 'Lift Grenade' High Level ability, which creates a much stronger explosion than 'Biotic Charge': enemies in a 15' radius suffer 20d4 crushing damage and are stunned for 6 seconds. A save vs. breath at -2 halves the damage and avoids the stun.

Disadvantages:
-  May not use missile weapons
-  May not wear armor
-  May not wear gauntlets or bracers
-  Cannot specialize in one-handed weapons
-  Cannot become proficient in any fighting style other than two-handed weapons~
